plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

// Include version-specific common for MC 1.21.x
useCommonMc121()

// Version-specific properties for MC 1.21.11
def mcVersion = rootProject.mc_12111_version
def neoforgeVersion = rootProject.mc_12111_neoforge

neoForge {
    version = neoforgeVersion
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge 1.21.11 ${it.name.capitalize()} (${project.path})"
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

repositories {
    mavenLocal()
    flatDir {
        dirs "${rootProject.projectDir}/libs"
    }
    maven {
        url 'https://maven.kosmx.dev/'
    }
    maven { url = "https://repo.redlance.org/public" }
}

dependencies {
    // Version-specific compat module
    implementation project(':compat-mc121x')

    // blade-ink from local libs folder (classes included in jar task)
    implementation files("${rootProject.projectDir}/libs/blade-ink-${blade_ink_version}.jar")
    compileOnly("io.github.kosmx.emotes:emotesAPI:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly("io.github.kosmx.emotes:emotesServer:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly("io.github.kosmx.emotes:emotesMc:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Custom JAR naming for version identification
tasks.named('jar') {
    archiveBaseName = "${mod_id}-neoforge-1.21.11"
    // Include blade-ink JAR classes in the final mod jar
    from(zipTree("${rootProject.projectDir}/libs/blade-ink-${blade_ink_version}.jar")) {
        exclude 'META-INF/**'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
