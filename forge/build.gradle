plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev.legacyforge'
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

legacyForge {
    version = "${minecraft_version}-${forge_version}"

    validateAccessTransformers = true

    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers = ["src/main/resources/META-INF/accesstransformer.cfg"]
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        client {
            client()
        }
        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    mavenLocal()
    maven {
        url 'https://maven.kosmx.dev/'
    }
    maven { url = "https://repo.redlance.org/public" }
}


dependencies {
    compileOnly project(":common")
    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
    jarJar(implementation("com.bladecoder.ink:blade-ink")) {
        version {
            prefer '1.2.1+nc'
        }
    }
    additionalRuntimeClasspath 'com.bladecoder.ink:blade-ink:1.2.1+nc'
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesAPI', version: "${emotesAPI_version}")
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesServer', version: "${emotesAPI_version}")
    implementation "org.jetbrains:annotations:26.0.2"
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}