plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url 'https://maven.kosmx.dev/'
    }
    maven { url = "https://repo.redlance.org/public" }
}

dependencies {
    jarJar(implementation("com.bladecoder.ink:blade-ink")) {
        version {
            prefer blade_ink_version
        }
    }
    implementation "com.bladecoder.ink:blade-ink:${blade_ink_version}"
    compileOnly("io.github.kosmx.emotes:emotesAPI:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly("io.github.kosmx.emotes:emotesServer:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly("io.github.kosmx.emotes:emotesMc:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }