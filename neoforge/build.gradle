plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            client()
        }
        data {
            clientData()
        }
        server {
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

repositories {
    mavenLocal()
    flatDir {
        dirs "${rootProject.projectDir}/libs"
    }
    maven {
        url 'https://maven.kosmx.dev/'
    }
    maven { url = "https://repo.redlance.org/public" }
}

dependencies {
    // blade-ink from local libs folder (classes included in jar task)
    implementation files("${rootProject.projectDir}/libs/blade-ink-${blade_ink_version}.jar")
    compileOnly("io.github.kosmx.emotes:emotesAPI:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly("io.github.kosmx.emotes:emotesServer:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
    compileOnly("io.github.kosmx.emotes:emotesMc:${emotesAPI_version}") {
        exclude group: 'com.google.guava', module: 'guava'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

// Include blade-ink JAR classes in the final mod jar
tasks.named('jar') {
    from(zipTree("${rootProject.projectDir}/libs/blade-ink-${blade_ink_version}.jar")) {
        exclude 'META-INF/**'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}